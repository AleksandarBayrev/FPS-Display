plugins {
    id "fabric-loom" version "1.1-SNAPSHOT"
    id "com.modrinth.minotaur" version "2.+"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "maven-publish"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

repositories {
    maven { url = "https://maven.shedaniel.me/" }
    maven { url = "https://maven.terraformersmc.com/" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    includeFabricApiModule "fabric-key-binding-api-v1"
    includeFabricApiModule "fabric-lifecycle-events-v1"

    modImplementation "io.github.prospector:modmenu:${project.modmenu_version}"

    modApi("me.shedaniel.cloth:config-2:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include("me.shedaniel.cloth:config-2:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi("me.sargunvohra.mcmods:autoconfig1u:${project.autoconfig_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include("me.sargunvohra.mcmods:autoconfig1u:${project.autoconfig_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modRuntimeOnly "com.github.astei:lazydfu:0.1.2"
}

def includeFabricApiModule(String moduleName) {
    Object dependency = fabricApi.module(moduleName, project.fabric_version)
    dependencies.modImplementation(dependency)
    dependencies.include(dependency)
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 8
    }
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE.txt"
}

// Configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // To publish to maven local use the task `publishToMavenLocal`.
    repositories {
        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    }
}

def ENV = System.getenv()

def getShortMcVersion() {
    return project.minecraft_version.substring(0, 4)
}

modrinth {
    token = ENV.MODRINTH_TOKEN

    projectId = "DIlqwRFH"
    versionNumber = "${project.mod_version}+${getShortMcVersion()}"
    versionName = "[${getShortMcVersion()}] FPS-Display v${project.mod_version}"
    versionType = "release"
    
    uploadFile = remapJar
    gameVersions = ["1.16.2", "1.16.3", "1.16.4", "1.16.5"]
    loaders = ["fabric"]
}

curseforge {
    if (ENV.CURSEFORGE_TOKEN) {
        apiKey = ENV.CURSEFORGE_TOKEN
    }

    project {
        id = "440228"
        changelog = "A changelog can be found at https://github.com/Grayray75/FPS-Display/commits/"
        releaseType = "release"
        addGameVersion "${project.minecraft_version}"
        addGameVersion "Fabric"

        mainArtifact(remapJar) {
            displayName = "[${getShortMcVersion()}] FPS-Display v${project.mod_version}"
        }

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
    }
}
